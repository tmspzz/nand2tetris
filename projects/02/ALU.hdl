// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=outZX);
    Not16(in=outZX, out=outNX);
    Mux16(a=outZX, b=outNX, sel=nx, out=processedX);
    Mux16(a=y, b=false, sel=zy, out=outZY);
    Not16(in=outZY, out=outNY);
    Mux16(a=outZY, b=outNY, sel=ny, out=processedY);
    Add16(a=processedX, b=processedY, out=xPLUSy);
    And16(a=processedX, b=processedY, out=xANDy);
    Mux16(a=xANDy, b=xPLUSy, sel=f, out=fout);
    Not16(in=fout, out=NOTfout);
    Mux16(a=fout, b=NOTfout, sel=no, out[0..7]=ffout07, out[8..15]=ffout815);
    And16(a[0..7]=ffout07, a[8..15]=ffout815, b=true, out[0..14]=out014, out[15]=msb);
    And16(a[0..14]=out014, a[15]=msb,  b=true, out=out);
    Or8Way(in=ffout07, out=or07);
    Or8Way(in=ffout815, out=or815);
    Or(a=or07, b=or815, out=lowZR);
    Not(in=lowZR, out=zr);
    And(a=msb, b=true, out=ng);
}